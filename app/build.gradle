plugins {
    id 'com.android.application' apply true
    id 'com.google.gms.google-services' apply true
    id 'com.google.firebase.crashlytics' apply true
    id 'com.google.android.gms.oss-licenses-plugin' apply true
    id 'kotlin-android' apply true
}

// See explanation here: https://stackoverflow.com/questions/20562189/sign-apk-without-putting-keystore-info-in-build-gradle
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 33
    namespace 'co.tinode.tindroid'

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    defaultConfig {
        applicationId "co.tinode.tindroidx"
        minSdkVersion 24
        targetSdkVersion 33
        vectorDrawables.useSupportLibrary = true

        // If you are getting errors due to git not being setup comment out lines gitVersion*()
        // and uncomment lines with static assignments.

        buildConfigField 'int', 'VERSION_CODE', "${rootProject.ext.version_code}"
        buildConfigField 'String', 'VERSION_NAME', "\"${rootProject.ext.version_name}\""

        versionCode rootProject.ext.version_code
        versionName rootProject.ext.version_name
    }

    buildTypes {
        debug {
            resValue "string", "default_host_name", '"sandbox.tinode.co"'
        }
        release {
            resValue "string", "default_host_name", '"api.tinode.co"'
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs.
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/NOTICE', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/NOTICE.txt']
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(path: ':tinodesdk')

    // Camera libraries must use the the same version.
    def camerax_version = '1.2.2'
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"

    // noinspection GradleDependency: does not compile with 1.7 (duplucate kotlin classes).
    implementation 'androidx.activity:activity:1.6.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'androidx.emoji2:emoji2:1.3.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.6'
    implementation 'androidx.fragment:fragment:1.5.6'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.work:work-runtime:2.8.1'

    implementation 'com.android.installreferrer:installreferrer:2.2'

    implementation 'com.google.android.exoplayer:exoplayer-core:2.18.5'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.18.5'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'com.google.firebase:firebase-core:21.1.1'
    implementation 'com.google.firebase:firebase-crashlytics:18.3.6'
    implementation 'com.google.firebase:firebase-messaging:23.1.2'
    implementation 'com.google.mlkit:barcode-scanning:17.1.0'

    implementation 'com.googlecode.libphonenumber:libphonenumber:8.13.8'

    // Don't change to 2.7182... The 2.8 is the latest.
    // noinspection GradleDependency
    implementation 'com.squareup.picasso:picasso:2.8'
    // WebRTC.
    implementation "io.livekit:livekit-android:1.1.10"
    // QR Code generator.
    implementation 'io.nayuki:qrcodegen:1.8.0'

    annotationProcessor 'androidx.lifecycle:lifecycle-common-java8:2.6.1'

    // noinspection GradleDependency: does not compile with 2.x (even for API 33).
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2'

    // Kotlin.
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.8.20'
}

configurations {
    all {
        exclude group: 'androidx.lifecycle', module: 'lifecycle-viewmodel-ktx'
    }
}